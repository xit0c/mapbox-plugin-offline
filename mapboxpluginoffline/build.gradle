apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

File secretPropsFile = file('../secret.properties')
Properties secretProps = new Properties()
if (secretPropsFile.exists()) secretProps.load(secretPropsFile.newDataInputStream())

String libVersionName = '1.1.7'
String javadocDir = "$buildDir/javadoc"
String docsDir = "$rootDir/docs"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName libVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "$archivesBaseName-$versionName"
    }
    dokka {
        outputFormat = 'gfm'
        outputDirectory = javadocDir
        linkMapping {
            dir = "./"
            url = "https://github.com/xit0c/mapbox-plugin-offline/tree/master/mapboxpluginoffline/"
            suffix = "#L" // suffix used to append the line number to the URL (#L for GitHub)
        }
        externalDocumentationLink {
            url = new URL("https://developer.android.com/reference/")
            packageListUrl = new URL("https://developer.android.com/reference/androidx/package-list")
        }
    }
    buildTypes {
        debug {
            buildConfigField('String', 'MAPBOX_ACCESS_TOKEN', '"' + secretProps.getProperty('mapbox.accessToken', '') + '"' )
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation "androidx.core:core-ktx:$androidx_core_version"

    // Mapbox
    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_version"

    // Testing libraries
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_test_espresso_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_junit_version"
}

publish {
    userOrg = ''
    repoName = 'mapbox-plugin-offline'
    groupId = 'dev.micheleferretti'
    artifactId = 'mapbox-plugin-offline'
    publishVersion = libVersionName
    desc = 'An alternative to Mapbox offline plugin, written in Kotlin.'
    website = 'https://github.com/xit0c/mapbox-plugin-offline'
    bintrayUser = secretProps.getProperty('bintray.user', '')
    bintrayKey = secretProps.getProperty('bintray.key', '')
    dryRun = false
}

task cleanDocs(type: Delete, group: 'Documentation') {
    println "Cleaning up $javadocDir and $docsDir"
    delete javadocDir
    delete docsDir
}

task copyDocs(type: Copy, group: 'Documentation') {
    println "Copying files from $javadocDir/$project.name to $docsDir"
    from "$javadocDir/$project.name"
    into docsDir
}